(ns sheets.ids
  "Identity helpers for spreadsheets (clj port of lg_sheets/ids.py).

  Canonical entity id = \"sheet:book:{slug}\" (the datomic entity ref).
  Path-based DID    = \"did:web:sheets.etzhayyim.com:spreadsheet:{slug}\".
  AT URI            = \"at://{did}/ai.etzhayyim.apps.sheets.spreadsheet/{slug}\"."
  (:require [clojure.string :as str]))

(def ^:private slug-alphabet "0123456789abcdefghijklmnopqrstuvwxyz")
(def ^:private domain "sheets.etzhayyim.com")
(def ^:private collection "ai.etzhayyim.apps.sheets.spreadsheet")

(defn new-slug
  "16-char nanoid-shaped slug over [0-9a-z] (matches nanoid alphabet/length)."
  []
  (apply str (repeatedly 16 #(rand-nth slug-alphabet))))

(defn eid-for-slug [slug] (str "sheet:book:" slug))

(defn slug-from-eid [eid]
  (last (str/split (str eid) #":")))

(defn did-for-slug [slug] (str "did:web:" domain ":spreadsheet:" slug))

(defn uri-for-slug [slug]
  (str "at://" (did-for-slug slug) "/" collection "/" slug))

(def ^:private slug-re #"^[0-9a-z]{6,32}$")

(defn resolve-slug [spreadsheet-id]
  (cond
    (str/blank? (or spreadsheet-id "")) nil
    (str/starts-with? spreadsheet-id "sheet:book:") (slug-from-eid spreadsheet-id)
    (and (str/starts-with? spreadsheet-id "did:web:")
         (str/includes? spreadsheet-id ":spreadsheet:"))
    (last (str/split spreadsheet-id #":spreadsheet:"))
    (re-matches slug-re spreadsheet-id) spreadsheet-id
    :else nil))
